(ignore x86-64 Linux specific constants.)

(ignore x86-64 Linux system-call numbers.)

  (constant sys-read #:0 \:r:(` syscall-num r))

  (constant sys-write #:1 \:r:(` syscall-num r))

  (constant sys-open #:2 \:r:(` syscall-num r))

  (constant sys-close #:3 \:r:(` syscall-num r))

  (constant sys-fstat #:5 \:r:(` syscall-num r))

  (constant sys-mmap #:9 \:r:(` syscall-num r))

  (constant sys-munmap #:11 \:r:(` syscall-num r))

  (constant sys-exit #:60 \:r:(` syscall-num r))

  (constant sys-clock-gettime #:228 \:r:(` syscall-num r))

(ignore Linux open access modes.)

  (constant o-rdonly #:0 \:r:[var r])

  (constant o-wronly #:1 \:r:[var r])

  (constant o-rdwr #:2 \:r:[var r])

  (constant o-creat #:64 \:r:[var r])

(ignore Linux open file modes.)

  (constant s-irwxu #:448 \:r:[var r])

  (constant s-irwxg #:56 \:r:[var r])

  (constant s-irwxo #:7 \:r:[var r])

(ignore Valid memory protections for mappings.)

  (constant prot-read #:1 \:r:[var r])

  (constant prot-write #:2 \:r:[var r])

  (constant prot-exec #:4 \:r:[var r])

  (constant prot-none #:0 \:r:[var r])

(ignore Valid flags for mmap.)

  (constant map-private #:2 \:r:[var r])

  (constant map-anonymous #:32 \:r:[var r])
  
  (constant map-32bit #:64 \:r:[var r])

(constant page-size #:4096 \:r:[var r])

(constant arg-max (page-size) \:r:[var r])

(constant clock-process-cputime-id #:2 \:r:[var r])

(constant stdin #:0 (\ r (` fd r)))

(constant stdout #:1 (\ r (` fd r)))

(constant byte #:1 \:r:[var r])

(constant unit #:8 \:r:[var r])

(constant alloc-alignment #:8 \:r:[var r])

(ignore Layout of the header of our endogenous linked list of mmaped regions.)

  (constant region-element #:48 \:r:[var r])
  
  (ignore Contains reference to previous mmaped region. Is null-element otherwise.)
  
  (property reg-prev-elt #:0 #:8 \:r:(`(! static~ region region)r))

  (ignore Contains reference to next mmaped region. Is null-element otherwise.)

  (property reg-next-elt #:8 #:8 \:r:(`(! static~ region region)r))

  (ignore Contains reference to last mmaped region in region. Field required to reduce allocation time.)

  (property reg-last-elt #:16 #:8 \:r:(`(! static~ region region)r))

  (ignore Contains reference to next aligned free memory in region element.)

  (property reg-curr-pos #:24 #:8 \:r:(`(! static~ ,:[var r] region)r))

  (ignore Contains reference to the end of the region element.)

  (property reg-end-pos #:32 #:8 \:r:(`(! static~ ,:[var r] region)r))

  (ignore Contains magic debug value.)

  (property reg-magic-dbg #:40 #:8 \:r:(`(! static~ ,:[var r] region)r))

(ignore Defined in assembly file that will be linked with this file.)

(constrain syscall (\ r (?(a b c d e f g) (`(function (syscall-num ,:a ,:b ,:c ,:d ,:e ,:f) ,:g)r)r)))

(function exit (code) [syscall (sys-exit) code (ignore) (ignore) (ignore) (ignore) (ignore)])

(function memcpy (dest src n)
  (loop do-cpy (dest dest) (src src) (n n)
    (if n
      (do
        [setb dest [getb src]]
        {do-cpy [+ dest #:1] [+ src #:1] [- n #:1]})
      (ignore))))

(function memset (s c n)
  (loop do-set (s s) (n n)
    (if n
      (do
        [setb s c]
        {do-set [+ s #:1] [- n #:1]})
      (ignore))))

(function isspace (c) (switch = c
  ((space)(ff)(lf)(cr)(ht)(vt) (true))
  (false)))

(ignore
  What follows is essentially wrappers for Linux syscalls. The only twist is that if the
  actual syscall returns an error code, then these functions will follow the error
  continuation instead of returning.)

(storage file-access-error (ignore))

(constrain open (\ r (?(b) (`(function (string ,:b) fd)r)r)))

(function open (path handler)
  (let (ret [syscall (sys-open) path (o-rdonly) (ignore) (ignore) (ignore) (ignore)])
    (if (and [<= [- #:0 #:4095] ret] [<= ret [- #:0 #:1]])
      {handler (storage _ file-access-error path)}
      ret)))

(constrain create (\ r (?(b) (`(function (string ,:b) fd)r)r)))

(function create (path handler)
  (let (ret [syscall (sys-open) path [bor (o-wronly) (o-creat)] [bor (s-irwxu) [bor (s-irwxg) (s-irwxo)]]  (ignore) (ignore) (ignore)])
    (if (and [<= [- #:0 #:4095] ret] [<= ret [- #:0 #:1]])
      {handler (storage _ file-access-error path)}
      ret)))

(constrain print (\ r (?(a b c e) (`(function (fd ,:a ,:b ,:c) ,:e)r)r)))

(function print (fd d len handler)
  (let (ret [syscall (sys-write) fd d len (ignore) (ignore) (ignore)])
    (if (and [<= [- #:0 #:4095] ret] [<= ret [- #:0 #:1]])
      {handler (storage _ file-access-error empty-string)}
      ret)))

(constrain print-str (\ r (?(b e) (`(function (fd string ,:b) ,:e)r)r)))

(function print-str (fd str handler) [print fd str [strlen str] handler])

(constrain print-char (\ r (?(a b e) (`(function (fd ,:a ,:b) ,:e)r)r)))

(function print-char (fd ch handler) [print fd (storage _ ch) #:1 handler])

(constrain dec->char (\ r (?(a b) (`(function (,:a) ,:b)r)r)))

(function dec->char (h)
  (switch = h
    (#:9 (char 9))
    (#:8 (char 8))
    (#:7 (char 7))
    (#:6 (char 6))
    (#:5 (char 5))
    (#:4 (char 4))
    (#:3 (char 3))
    (#:2 (char 2))
    (#:1 (char 1))
    (char 0)))

(constrain print-uint (\ r (?(a b e) (`(function (fd ,:a ,:b) ,:e)r)r)))

(function print-uint (fd i handler)
  (let (j (storage _ #:19))
  (let (str (storage _ (ignore) (ignore) (ignore))) (do
  (loop next-digit (i i)
    (if [= i #:0] (ignore) (do
      [setb [+ str $:j] [dec->char [rem i #:10]]]
      [set j [- $:j #:1]]
      {next-digit [/ i #:10]})))
  [print fd [+ str [+ $:j #:1]] [- #:19 $:j] handler]))))

(constrain hex->char (\ r (?(a b) (`(function (,:a) ,:b)r)r)))

(function hex->char (h)
  (switch = h
    (0x:F (char F))
    (0x:E (char E))
    (0x:D (char D))
    (0x:C (char C))
    (0x:B (char B))
    (0x:A (char A))
    (0x:9 (char 9))
    (0x:8 (char 8))
    (0x:7 (char 7))
    (0x:6 (char 6))
    (0x:5 (char 5))
    (0x:4 (char 4))
    (0x:3 (char 3))
    (0x:2 (char 2))
    (0x:1 (char 1))
    (char 0)))

(constrain print-hex (\ r (?(a b e) (`(function (fd ,:a ,:b) ,:e)r)r)))

(function print-hex (fd i handler)
  (let (j (storage _ #:16))
  (let (str (storage _ (ignore) (ignore))) (do
  (loop next-digit (i i)
    (if [= i #:0] (ignore) (do
      [setb [+ str $:j] [hex->char [rem i #:16]]]
      [set j [- $:j #:1]]
      {next-digit [/ i #:16]})))
  [print fd [+ str [+ $:j #:1]] [- #:16 $:j] handler]))))

(constrain print-bool (\ r (?(a b) (`(function (fd ,:a ,:b) ,:a)r)r)))

(function print-bool (fd i handler) (do
  [print-str fd (if i (" T) (" F)) handler]
  i))

(constrain print-bytes
  (\ r (?(a b h i) (`(function (fd ,:a ,:b ,:h) ,:i)r)r)))

(function print-bytes (fd bs sz handler)
  (for (i (range #:0 sz)) (ignore) (do
    [print-char fd [hex->char [>> [getb[+ bs i]] #:4]] handler]
    [print-char fd [hex->char [band [getb[+ bs i]] 0x:0F]] handler])))

(constrain read (\ r (?(a b c e) (`(function (fd ,:a ,:b ,:c) ,:e)r)r)))

(function read (fd buf cnt handler)
  (let (ret [syscall (sys-read) fd buf cnt (ignore) (ignore) (ignore)])
    (if (and [<= [- #:0 #:4095] ret] [<= ret [- #:0 #:1]])
      {handler (storage _ file-access-error empty-string)}
      ret)))

(constrain close (\ r (?(a c) (`(function (fd ,:a) ,:c)r)r)))

(function close (fd handler)
  (let (ret [syscall (sys-close) fd (ignore) (ignore) (ignore) (ignore) (ignore)])
    (if (and [<= [- #:0 #:4095] ret] [<= ret [- #:0 #:1]])
      {handler (storage _ file-access-error empty-string)}
      ret)))

(constrain size (\ r (?(a c) (`(function (fd ,:a) ,:c)r)r)))

(function size (fd handler)
  (let (statbuf (storage _ (ignore)(ignore)(ignore)(ignore)(ignore)(ignore)(ignore)(ignore)(ignore)
    (ignore)(ignore)(ignore)(ignore)(ignore)(ignore)(ignore)(ignore)(ignore)))
  (let (ret [syscall (sys-fstat) fd statbuf (ignore) (ignore) (ignore) (ignore)])
    (if (and [<= [- #:0 #:4095] ret] [<= ret [- #:0 #:1]])
      {handler (storage _ file-access-error empty-string)}
      [get [+ statbuf #:48]]))))

(storage memory-access-error (ignore))

(ignore Creates a new virtual address mapping of the size in bytes given at len.)

(function mmap (len handler)
  (let (ret [syscall (sys-mmap) #:0 len [bor (prot-read) (prot-write)] [bor (map-private) [bor (map-anonymous) (map-32bit)]] [bnot #:0] #:0])
    (if (and [<= [- #:0 #:4095] ret] [<= ret [- #:0 #:1]])
      {handler (storage _ memory-access-error [- #:0 ret])}
      ret)))

(ignore
  Deletes virtual address mappings coinciding with the range starting with the value at
  ptr and having an extent of the value given at len.)

(function munmap (ptr len handler)
  (let (ret [syscall (sys-munmap) ptr len (ignore) (ignore) (ignore) (ignore)])
    (if (and [<= [- #:0 #:4095] ret] [<= ret [- #:0 #:1]])
      {handler (storage _ memory-access-error)}
      ret)))

(function pad-size (x nearest) (let (r [rem x nearest]) [+ x (if r [- nearest r] #:0)]))

(constrain null-element (\ r (` region r)))

(storage null-element (ignore))

(ignore Creates a new region with a minimum capacity in bytes given by the value at
  min-capacity.)

(constrain create-region (\ r (?(a b) (`(function (,:a ,:b) region)r)r)))

(function create-region (min-capacity handler)
  (let (len [pad-size [max [+ min-capacity (region-element)] #:1048576] (page-size)])
    (let (reg [mmap len handler]) (do
      (setf reg-next-elt reg null-element)
      (setf reg-prev-elt reg null-element)
      (setf reg-last-elt reg reg)
      (setf reg-curr-pos reg [+ reg (region-element)])
      (setf reg-end-pos reg [+ reg len])
      (setf reg-magic-dbg reg 0x:DEADBEEFDEADBEEF)
      reg))))

(ignore
  Allocates memory in the region at reg of an amount in bytes equal to the integer at len.)

(constrain region-alloc (\ r (?(a b d) (`(function (,:a region ,:b) ,:d)r)r)))

(function region-alloc (len reg handler)
  (let (len [pad-size len (alloc-alignment)])
  (let (new-last-elt
    (let (curr-last-elt (@ reg-last-elt reg))
    (if [> [+(@ reg-curr-pos curr-last-elt)len] (@ reg-end-pos curr-last-elt)]
      (let (new-last-elt [create-region len handler]) (do
        (setf reg-next-elt curr-last-elt new-last-elt)
        (setf reg-last-elt reg new-last-elt)
        (setf reg-prev-elt new-last-elt curr-last-elt)
        new-last-elt))
      curr-last-elt))) (do
  (setf reg-curr-pos new-last-elt [+(@ reg-curr-pos new-last-elt)len])
  [-(@ reg-curr-pos new-last-elt)len]))))

(ignore
  The following macro implements the following transformation:
  ((region-storage buf hdl) buf-storage arg1 arg2 ... argN)
  ->
  (let (buf-storage [region-alloc [* N(unit)] buf hdl]) (do
    [set [+ buf-storage [* #:0(unit)]] arg1]
    [set [+ buf-storage [* #:1(unit)]] arg2]
    ...
    [set [+ buf-storage [* #(N-1)(unit)]] argN]
    buf-storage)))

(function region-storage (region-storage.l region-storage.r)
  (`(function region-storage.temp (l r)
      (`(let ((,[@fst l]) [region-alloc [* (,[=# [- [meta.length l] #:1] r]) (unit)]
            (,(,[`[llst [@fst region-storage.l] (` r region-storage.r) emt region-storage.r] region-storage.r]))
            (,(,[`[llst [@frst region-storage.l] (` r region-storage.r) emt region-storage.r] region-storage.r]))])
          (,[lst (` do r) [meta.reverse [lst [@fst l]
            (loop make-set (identifiers [@rst l]) (idx #:0) (sets emt)
              (if [emt? identifiers]
                sets
                {make-set [@rst identifiers] [+ idx #:1]
                  [lst (`[set [+ (,[@fst l]) [* (,[=# idx r]) (unit)]] (,[@fst identifiers])]r) sets r]})) r] r] r]))r))region-storage.r))

(ignore
  Frees the given reference and everything allocated afterwards in the given region.)

(constrain region-free (\ r (?(a b d) (`(function (,:a region ,:b) ,:d)r)r)))

(function region-free (ref reg handler)
  (loop do-free (last-elt (@ reg-last-elt reg))
    (if (and [<= [+ last-elt (region-element)] ref] [<= ref (@ reg-end-pos last-elt)])
      (do
        (setf reg-curr-pos last-elt ref)
        (setf reg-last-elt reg last-elt)
        (setf reg-next-elt last-elt null-element))
      (let (prev-elt (@ reg-prev-elt last-elt)) (do
        [munmap last-elt [- (@ reg-end-pos last-elt) last-elt] handler]
        {do-free prev-elt})))))

(ignore
  A macro to ensure that allocated memory is freed. The following macro saves the supplied
  region's current position, evaluates the given expression, and then resets the region's
  position to the saved value. It is implemented by the following transformation:
  (with-region buf expr)
  ->
  (let (with-region.position [region-alloc #:0 buf (ignore)])
    (let (with-region.val expr) (do
      [region-free with-region.position buf (ignore)]
      with-region.val))))

(function with-region (l r)
  (`(let (with-region.region (,[@car l]))
      (let (with-region.position [region-alloc #:0 with-region.region (ignore)])
        (let (with-region.val (,[@cadr l])) (do
          [region-free with-region.position with-region.region (ignore)]
          with-region.val))))r))

(ignore
  Releases the memory associated with the region reg. After a call to this function, it is
  invalid to read or write data to the memory allocated on this region.)

(constrain destroy-region (\ r (?(b) (`(function (region ,:b) ())r)r)))

(function destroy-region (reg handler)
  (loop do-destroy (reg reg)
    (let (next-reg (@ reg-next-elt reg)) (do
      [munmap reg [- (@ reg-end-pos reg) reg] handler]
      (if [= next-reg null-element]
        (ignore)
        {do-destroy next-reg})))))

(ignore
  Returns a list containing the the command line arguments. The returned list and its
  dependencies will all reside in the region reg.)

(constrain argv (\ r (?(a) (`(function (region ,:a) (list string))r)r)))

(function argv (reg handler)
  (let (f [open (" /proc/self/cmdline) handler])
    (fallthrough
        (let (buf [region-alloc (arg-max) reg cleanup])
        (let (bytes-rem [read f buf (arg-max) cleanup])
        (storage _ no-error
          (loop get-arg (buf buf) (bytes-rem bytes-rem) (args nil)
            (if bytes-rem
              {get-arg [+ buf [+ [strlen buf] #:1]] [- bytes-rem [+ [strlen buf] #:1]] [cons buf args reg cleanup]}
              [reverse args reg cleanup])))))
      
      (cleanup err (do
        [close f handler]
        (if [= (@0 err) no-error] (@1 err) {handler err}))))))

(storage time-error (ignore))

(constrain clock-gettime (\ r (?(a b) (`(function (,:a) ,:b)r)r)))

(function clock-gettime (handler)
  (let (t (storage _ (ignore)(ignore)))
    (let (ret [syscall (sys-clock-gettime) (clock-process-cputime-id) t (ignore) (ignore) (ignore) (ignore)])
      (if (and [<= [- #:0 #:4095] ret] [<= ret [- #:0 #:1]])
        {handler (storage _ time-error)}
        [+ [* $:t #:1000000000] ($[+ t (unit)])]))))

(ignore
  Times the execution of the given expression in nanoseconds and adds it
  to the given accumulator. This expression evaluates to the values of its
  inner expression. Does the following transformation:
  
  (time acc expr handler)
  ->
  (let (time.handler handler)
    (let (time.start [clock-gettime time.handler])
      (let (time.val expr) (do
        [set+ acc [- [clock-gettime time.handler] time.start]]
        time.val)))))

(function time (l r)
  (`(let (time.handler (,[@frrst l]))
      (let (time.start [clock-gettime time.handler])
        (let (time.val (,[@frst l])) (do
          [set+ (,[@fst l]) [- [clock-gettime time.handler] time.start]]
          time.val))))r))
