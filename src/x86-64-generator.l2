(function word-size (l r) [=# #8 r])

(ignore Offsets for the register contents in the continuation buffer.)

  (function cont-size (l r) [=# [* #7 (word-size)] r])

  (function cont-r15 (l r) [=# [* #6 (word-size)] r])

  (function cont-r12 (l r) [=# [* #5 (word-size)] r])

  (function cont-rbx (l r) [=# [* #4 (word-size)] r])

  (function cont-r13 (l r) [=# [* #3 (word-size)] r])

  (function cont-r14 (l r) [=# [* #2 (word-size)] r])

  (function cont-cir (l r) [=# [* #1 (word-size)] r])

  (function cont-rbp (l r) [=# [* #0 (word-size)] r])

(function callee-saved-x64-r64s-len (l r) [=# #5 r])

(storage callee-saved-x64-r64s (rbx)(r12)(r13)(r14)(r15))

(ignore Looks up the (non-register) memory storage associated with a given context.)

(constrain context->memory-storage (\ r (`(function (bndg-context) bndg-storage)r)))

(function context->memory-storage (context)
  (switch = context
    ((intra-function-context) (frame-relative-storage))
    ((extra-function-context) (absolute-storage))
    (ignore)))

(ignore
  Storage expressions and escaping continuations need to store data that persists beyond
  the evaluation of the expression. That is, the buffers associated with the
  aforementioned expressions cannot be overwritten. Hence we need to place them in
  distinct locations.)

(constrain place-perm-bindings (\ r (with-vars (a) (`(function (expr ,a) ,a)r)r)))

(function place-perm-bindings (n offset)
  (switch = (@ expr-type n)
    ((-function-) (do
      (setf expr-frame-size n [place-perm-bindings (@ expr-expression n) [- #0 [* (callee-saved-x64-r64s-len)(word-size)]]])
      offset))
    
    ((-constrain-)
      [place-perm-bindings (@ expr-expression n) offset])
    
    ((-invoke-) (-jump-)
      (let (offset [place-perm-bindings (@ expr-target n) offset])
      (fold (offset offset) (u (@ expr-arguments n)) [place-perm-bindings u offset])))
    
    ((-if-)
      (let (offset [place-perm-bindings (@ expr-condition n) offset])
      (let (offset [place-perm-bindings (@ expr-consequent n) offset])
      (let (offset [place-perm-bindings (@ expr-alternate n) offset])
        offset))))
    
    ((-continuation-) (-with-)
      (if (@ expr-escapes n)
        (let (offset [- offset [* [length (@ expr-parameters n)] (word-size)]]) (do
          (let (offset-ref (storage _ offset))
          (foreach (t (@ expr-parameters n)) (do
            (setf bndg-preferred-storage t [context->memory-storage (@ bndg-context t)])
            (setf bndg-preferred-offset t $offset-ref)
            (setf bndg-size t (word-size))
            [set+ offset-ref (word-size)])))
          (let (cont-bndg (@ expr-reference n)) (offset [- offset (cont-size)]) (do
          (setf bndg-storage cont-bndg [context->memory-storage (@ bndg-context cont-bndg)])
          (setf bndg-size cont-bndg (cont-size))
          (setf bndg-offset cont-bndg offset)
          [place-perm-bindings (@ expr-expression n) offset]))))
        (let (cont-bndg (@ expr-reference n)) (do
          (setf bndg-storage cont-bndg [context->memory-storage (@ bndg-context cont-bndg)])
          (setf bndg-size cont-bndg #0)
          (setf bndg-offset cont-bndg offset)
          [place-perm-bindings (@ expr-expression n) offset]))))
    
    ((-storage-)
      (let (storage-size [* [length (@ expr-arguments n)] (word-size)])
      (let (offset [- offset storage-size]) (do
      (setf bndg-size (@ expr-reference n) storage-size)
      (setf bndg-offset (@ expr-reference n) offset)
      (setf bndg-storage (@ expr-reference n) [context->memory-storage (@ bndg-context (@ expr-reference n))])
      (let (offset-ref (storage _ offset))
      (foreach (u (@ expr-arguments n)) (do
        (setf bndg-size (@ expr-binding-aug u) (word-size))
        (setf bndg-preferred-offset (@ expr-binding-aug u) $offset-ref)
        (setf bndg-preferred-storage (@ expr-binding-aug u) [context->memory-storage (@ bndg-context (@ expr-binding-aug u))])
        [set+ offset-ref (word-size)])))
      (fold (offset offset) (u (@ expr-arguments n)) [place-perm-bindings u offset])))))
    
    offset))

(ignore
  Checks whether the given register is in use according to the given register usage
  vector.)

(constrain register-in-use? (\ r (`(function (register reg-use-vec) ())r)))

(function register-in-use? (reg reg-use-vec)
  (let (reg-idx [band reg (hex F)])
  (if [band [<< #1 reg-idx] reg-use-vec] (true) (false))))

(ignore
  Takes a binding, a register to assign it to, and a bit-vector indicating the registers
  already in use. Assigns the given binding to the given register if it is not already in
  use and returns an indication to that effect, otherwise it fails.)

(function try-bndg-to-preferred-location (bndg in-use)
  (switch = (@ bndg-preferred-storage bndg)
    ((nil-storage) in-use)
    
    ((frame-relative-storage) (absolute-storage) (do
      (setf bndg-storage bndg (@ bndg-preferred-storage bndg))
      (setf bndg-offset bndg (@ bndg-preferred-offset bndg))
      in-use))
      
    ((register-storage)
      (let (reg (@ bndg-preferred-offset bndg))
      (let (reg-idx [band reg (hex F)])
      (let (reg-occupied? [band [<< #1 reg-idx] in-use])
      (if (or reg-occupied? [= reg (none)])
        in-use
        (do
          (setf bndg-size bndg (word-size))
          (setf bndg-storage bndg (register-storage))
          (setf bndg-offset bndg reg)
          [bor [<< #1 reg-idx] in-use]))))))
          
    (ignore)))

(function try-expr-to-preferred-location (n in-use)
  [try-bndg-to-preferred-location (@ expr-binding-aug n) in-use])

(ignore
  Takes a binding, the current offset in the stack frame, and a bit-vector indicating the
  registers that are currently in use. Returns the new stack offset and register usage
  bit-vector after the given bit-vector is placed.)

(constrain binding-to-register/memory (\ r (with-vars (a) (`(function (bndg-aug ,a reg-use-vec) reg-use-vec)r)r)))

(function binding-to-register/memory (bndg offset in-use)
  (if [= in-use (hex FFFFFFFFFFFFFFFF)]
    (do
      (setf bndg-size bndg (word-size))
      (setf bndg-storage bndg [context->memory-storage (@ bndg-context bndg)])
      (setf bndg-offset bndg offset)
      in-use)
    (do
      (setf bndg-size bndg (word-size))
      (setf bndg-storage bndg (register-storage))
      (let (free-register-idx [lb [bnot in-use]])
      (let (free-register ($(loc (unit) free-register-idx x64-r64s)))
      (let (new-in-use [bor in-use [<< #1 free-register-idx]]) (do
      (setf bndg-offset bndg free-register)
      new-in-use)))))))

(function expr-to-register/memory (n offset in-use)
  [binding-to-register/memory (@ expr-binding-aug n) offset in-use])

(function bndg-assigned? (bndg) [not= (@ bndg-storage bndg) (nil-storage)])

(function expr-assigned? (expr) [bndg-assigned? (@ expr-binding-aug expr)])

(constrain initializer->function
  (\ r (with-vars (b c) (`(function (initializer)
    (function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c))r)r)))

(function initializer->function (x) [cast x])

(constrain function->initializer
  (\ r (with-vars (b c) (`(function
    ((function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)) initializer)r)r)))

(function function->initializer (x) [cast x])

(function initialize-expressions (exprs bndgs offset mask inits ret-val)
  (let (sub-expr-offset [- offset [* [+ [length exprs] [length bndgs]] (word-size)]])
  (bind (bottom-offset in-use)
    (loop init-subs (bottom-offset sub-expr-offset) (in-use mask) (u exprs)
      (if [nil? u]
        (values (use-storage bind) bottom-offset in-use)
        (bind (bottom-offset' in-use') [initialize-expression [@car u] sub-expr-offset inits bind]
          {init-subs [min bottom-offset bottom-offset'] [bor in-use in-use'] [@cdr u]})))
  (let (in-use (fold (in-use in-use) (u exprs) [try-expr-to-preferred-location u in-use]))
  (let (in-use (fold (in-use in-use) (u bndgs) [try-bndg-to-preferred-location u in-use]))
  (let (in-use
    (loop assign-args (offset [- offset [* [length exprs] (word-size)]]) (in-use in-use) (u exprs)
      (if [nil? u]
        in-use
        {assign-args [+ offset (word-size)]
          (if [expr-assigned? [@car u]] in-use [expr-to-register/memory [@car u] offset in-use]) [@cdr u]})))
  (let (in-use
    (loop assign-args (offset [- offset [* [+ [length exprs] [length bndgs]] (word-size)]]) (in-use in-use) (u bndgs)
      (if [nil? u]
        in-use
        {assign-args [+ offset (word-size)]
          (if [bndg-assigned? [@car u]] in-use [binding-to-register/memory [@car u] offset in-use]) [@cdr u]})))
  (values (use-storage ret-val) bottom-offset in-use))))))))

(constrain generate-function
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-function (n c relocs r handler)
  (substitute (.. r handler)
    (and [= (@ expr-type n) (-function-)] (let
        (ref-val-bndg (@ expr-reference n))
        (expr-val-bndg (@ expr-binding-aug n))
        (body-val-bndg (@ expr-binding-aug (@ expr-expression n)))
        (after-binding [make-binding-aug (extra-function-context) (absolute-storage) (local-scope) (defined-state) null-string null-frag null-expr ..]) (do
      
      [prepend(make-instrN(x86-lea)([bndg->op expr-val-bndg relocs ..][bndg->op ref-val-bndg relocs ..][make-reg-op(r11)..])..) c ..]
      [prepend (make-instrN (x86-jmp)([make-relative-relocation-immediate #4 after-binding relocs ..])..) c ..]
      [prepend [make-label ref-val-bndg ..] c ..]
      [prepend (make-instrN(x86-push)([make-reg-op (rbp) ..])..) c ..]
      [prepend (make-instrN(x86-mov)([make-reg-op (rbp) ..] [make-reg-op (rsp) ..])..) c ..]
      [prepend (make-instrN(x86-sub)([make-reg-op (rsp) ..] [make-imm-op #4 [- #0 (@ expr-frame-size n)] ..])..) c ..]
      
      (let (save-offset [- #0 [* (callee-saved-x64-r64s-len)(word-size)]])
      (until (i #0) [= i (callee-saved-x64-r64s-len)] [+ i #1]
        (let (current-register ($(loc(word-size)i callee-saved-x64-r64s)))
          (if [register-in-use? current-register (@ expr-registers-used n)]
            [prepend (make-instrN (x86-mov)
              ([make-mem-op(word-size)(rbp)(none)#0[+ save-offset[* i(word-size)]]#4 ..]
              [make-reg-op current-register ..]) ..) c ..]
            (ignore)))))
       
      (ignore Move first 6 parameters to their slots)
      (and [> [length (@ expr-parameters n)] #0] (do
        [generate-bndg-register-move [@car(@ expr-parameters n)] (rdi) c relocs ..]
      (and [> [length (@ expr-parameters n)] #1] (do
        [generate-bndg-register-move [@cadr(@ expr-parameters n)] (rsi) c relocs ..]
      (and [> [length (@ expr-parameters n)] #2] (do
        [generate-bndg-register-move [@caddr(@ expr-parameters n)] (rdx) c relocs ..]
      (and [> [length (@ expr-parameters n)] #3] (do
        [generate-bndg-register-move [@cadddr(@ expr-parameters n)] (rcx) c relocs ..]
      (and [> [length (@ expr-parameters n)] #4] (do
        [generate-bndg-register-move [@caddddr(@ expr-parameters n)] (r8) c relocs ..]
      (and [> [length (@ expr-parameters n)] #5]
        [generate-bndg-register-move [@cadddddr(@ expr-parameters n)] (r9) c relocs ..])))))))))))
      
      (let (rem-offset [* #2(word-size)])
      (let (rem-params (if [>= [length (@ expr-parameters n)] #6] [@cddddddr (@ expr-parameters n)] nil))
      (fold (offset rem-offset) (u rem-params) (do
        [prepend (make-instrN(x86-mov)
          ([bndg->op u relocs ..][make-mem-op(word-size)(rbp)(none)#0 offset #4 ..][make-reg-op(r11)..])..) c ..]
        [+ offset (word-size)]))))
      
      (ignore Execute the function body)
      [generate-expression (@ expr-expression n) c relocs ..]
      [generate-register-bndg-move (rax) body-val-bndg c relocs ..]
      
      (let (save-offset [- #0 [* (callee-saved-x64-r64s-len)(word-size)]])
      (until (i #0) [= i (callee-saved-x64-r64s-len)] [+ i #1]
        (let (current-register ($(loc(word-size)i callee-saved-x64-r64s)))
          (if [register-in-use? current-register (@ expr-registers-used n)]
            [prepend (make-instrN (x86-mov)
              ([make-reg-op current-register ..]
              [make-mem-op(word-size)(rbp)(none)#0[+ save-offset[* i(word-size)]]#4 ..]) ..) c ..]
            (ignore)))))
            
      [prepend (make-instrN(x86-leave)()..) c ..]
      [prepend (make-instrN(x86-ret)()..) c ..]
      [prepend [make-label after-binding ..] c ..]
      (true))))))

(constrain initialize-function (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-function (n offset inits ret-val)
  (and [= (@ expr-type n) (-function-)] (do
    (let (call-regs (storage _ (rdi)(rsi)(rdx)(rcx)(r8)(r9)))
      (loop set-prefs (rem-params (@ expr-parameters n)) (i #0)
        (if (or [= i #6] [nil? rem-params]) (ignore) (do
          (setf bndg-preferred-storage [@car rem-params] (register-storage))
          (setf bndg-preferred-offset [@car rem-params] ($(loc (unit) i call-regs)))
          {set-prefs [@cdr rem-params] [+ i #1]}))))
    
    (let (rem-offset [* #2(word-size)])
    (let (rem-params (if [>= [length (@ expr-parameters n)] #6] [@cddddddr (@ expr-parameters n)] nil))
    (loop assign-params (offset rem-offset) (u rem-params)
      (if [nil? u] (ignore) (do
        (setf bndg-preferred-storage [@car u] (frame-relative-storage))
        (setf bndg-preferred-offset [@car u] offset)
        {assign-params [+ offset (word-size)] [@cdr u]})))))
    
    (bind (frame-size in-use)
      [initialize-expressions (storage-cons (@ expr-expression n) nil) (@ expr-parameters n)
        (@ expr-frame-size n) (hex FFFFFFFFFFFF0831) inits ret-val] (do
    (setf expr-registers-used n in-use)
    (setf expr-frame-size n frame-size)
    (foreach (t (@ expr-parameters n)) (setf bndg-size t (word-size)))))
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (setf expr-generator n [function->generator generate-function])
    (values (use-storage ret-val) offset (hex 0000000000000800))
    (true))))

(constrain generate-store-continuation (\ r (with-vars (a) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ())r)r)))

(function generate-store-continuation (n c relocs r handler)
  (substitute (.. r handler) (let
      (cont-val-bndg (@ expr-reference n))
      (cont-instr-bndg (@ expr-cont-instr-bndg n)) (do
    [prepend (make-instrN (x86-lea) ([make-reg-op (r11) ..] [bndg->op cont-val-bndg relocs ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-mem-op(word-size)(r11)(none)#0(cont-rbx)#4 ..] [make-reg-op (rbx) ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-mem-op(word-size)(r11)(none)#0(cont-r12)#4 ..] [make-reg-op (r12) ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-mem-op(word-size)(r11)(none)#0(cont-r13)#4 ..] [make-reg-op (r13) ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-mem-op(word-size)(r11)(none)#0(cont-r14)#4 ..] [make-reg-op (r14) ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-mem-op(word-size)(r11)(none)#0(cont-r15)#4 ..] [make-reg-op (r15) ..]) ..) c ..]
    [prepend (make-instrN (x86-lea) ([make-mem-op(word-size)(r11)(none)#0(cont-cir)#4 ..] [bndg->op cont-instr-bndg relocs ..] [make-reg-op (r10) ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-mem-op(word-size)(r11)(none)#0(cont-rbp)#4 ..] [make-reg-op (rbp) ..]) ..) c ..]))))

(constrain generate-continuation
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-continuation (n c relocs r handler)
  (substitute (.. r handler)
    (and [= (@ expr-type n) (-continuation-)]
      (let
          (cont-val-bndg (@ expr-reference n))
          (expr-val-bndg (@ expr-binding-aug n))
          (after-binding [make-binding-aug (extra-function-context) (absolute-storage) (local-scope) (defined-state) null-string null-frag null-expr ..]) (do
        (if (@ expr-escapes n) (do
          [prepend (make-instrN(x86-lea)([bndg->op expr-val-bndg relocs ..][bndg->op cont-val-bndg relocs ..][make-reg-op (r11) ..]) ..) c ..]
          [generate-store-continuation n c relocs ..])
          (ignore))
        
        [prepend (make-instrN (x86-jmp) ([make-relative-relocation-immediate #4 after-binding relocs ..]) ..) c ..]
        [prepend [make-label (@ expr-cont-instr-bndg n) ..] c ..]
        [generate-expression (@ expr-expression n) c relocs ..]
        [prepend [make-label after-binding ..] c ..]
        (true))))))

(constrain initialize-continuation (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-continuation (n offset inits ret-val)
  (and [= (@ expr-type n) (-continuation-)] (do
    [initialize-expressions (storage-cons (@ expr-expression n) nil) (@ expr-parameters n)
      offset (hex FFFFFFFFFFFF0C38) inits ret-val]
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (setf expr-generator n [function->generator generate-continuation])
    (true))))

(constrain generate-with
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-with (n c relocs r handler)
  (substitute (.. r handler)
    (and [= (@ expr-type n) (-with-)]
      (let
          (expr-val-bndg (@ expr-binding-aug n))
          (body-val-bndg (@ expr-binding-aug (@ expr-expression n)))
          (param-val-bndg [@car(@ expr-parameters n)])
          (end-instr-binding
            [make-binding-aug (extra-function-context) (absolute-storage) (local-scope) (defined-state) null-string null-frag null-expr ..]) (do
        (if (@ expr-escapes n) [generate-store-continuation n c relocs ..] (ignore))
        [generate-expression (@ expr-expression n) c relocs ..]
        (if (@ expr-escapes n)
          (do
            [prepend (make-instrN (x86-jmp) ([make-relative-relocation-immediate #4 end-instr-binding relocs ..]) ..) c ..]
            [prepend [make-label (@ expr-cont-instr-bndg n) ..] c ..]
            [generate-bndg-bndg-move expr-val-bndg param-val-bndg (r11) c relocs ..]
            [prepend [make-label end-instr-binding ..] c ..])
          [prepend [make-label (@ expr-cont-instr-bndg n) ..] c ..])
        (true))))))

(constrain initialize-with (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-with (n offset inits ret-val)
  (and (or [= (@ expr-type n) (-with-)]) (do
    (setf expr-binding-aug (@ expr-expression n) (@ expr-binding-aug n))
    (bind (frame-size in-use) [initialize-expression (@ expr-expression n) offset inits bind] (do
    (if (@ expr-escapes n) (ignore)
      (let (cont-bndg (@ expr-reference n)) (do
      (setf bndg-size cont-bndg #0)
      (setf bndg-offset cont-bndg offset)
      [set (& car (@ expr-parameters n)) (@ expr-binding-aug n)])))
    (let (in-use [bor (hex FFFFFFFFFFFF0C38) in-use]) (do
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (setf expr-generator n [function->generator generate-with])
    (values (use-storage ret-val) frame-size in-use)
    (true))))))))

(constrain generate-storage
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-storage (n c relocs r handler)
  (substitute (.. r handler)
    (and [= (@ expr-type n) (-storage-)]
      (let
          (expr-val-bndg (@ expr-binding-aug n))
          (storage-val-bndg (@ expr-reference n)) (do
        (foreach (t (@ expr-arguments n)) [generate-expression t c relocs ..])
        [prepend(make-instrN(x86-lea)
          ([bndg->op expr-val-bndg relocs ..][bndg->op storage-val-bndg relocs ..][make-reg-op(r11) ..])..) c ..]
        (true))))))

(constrain initialize-storage (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-storage (n offset inits ret-val)
  (and [= (@ expr-type n) (-storage-)] (do
    [initialize-expressions (@ expr-arguments n) nil offset (hex FFFFFFFFFFFF0C38) inits ret-val]
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (setf expr-generator n [function->generator generate-storage])
    (true))))

(constrain generate-buffer-to-dest
  (\ r (with-vars (a b) (`(function (expr register ,a (ref (list instr)) (ref (list reloc)) buffer ,b) ())r)r)))

(function generate-buffer-to-dest (n dest-reg offset c relocs r handler)
  (substitute (.. r handler)
    (let (dest-offset (storage _ offset))
      (foreach (t (@ expr-arguments n)) (let
          (arg-val-bndg (@ expr-binding-aug t)) (do
        [prepend (make-instrN (x86-mov)
          ([make-mem-op(word-size)dest-reg(none)#0 $dest-offset #4 ..][bndg->op arg-val-bndg relocs ..][make-reg-op(r11)..]) ..) c ..]
        [set+ dest-offset (word-size)]))))))

(constrain generate-load-continuation
  (\ r (with-vars (a) (`(function (register (ref (list instr)) (ref (list reloc)) buffer ,a) ())r)r)))

(function generate-load-continuation (src-reg c relocs r handler)
  (substitute (.. r handler) (do
    [prepend (make-instrN (x86-mov) ([make-reg-op (rbx) ..] [make-mem-op(word-size)src-reg(none)#0(cont-rbx)#4 ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-reg-op (r12) ..] [make-mem-op(word-size)src-reg(none)#0(cont-r12)#4 ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-reg-op (r13) ..] [make-mem-op(word-size)src-reg(none)#0(cont-r13)#4 ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-reg-op (r14) ..] [make-mem-op(word-size)src-reg(none)#0(cont-r14)#4 ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-reg-op (r15) ..] [make-mem-op(word-size)src-reg(none)#0(cont-r15)#4 ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-reg-op (r11) ..] [make-mem-op(word-size)src-reg(none)#0(cont-cir)#4 ..]) ..) c ..]
    [prepend (make-instrN (x86-mov) ([make-reg-op (rbp) ..] [make-mem-op(word-size)src-reg(none)#0(cont-rbp)#4 ..]) ..) c ..]
    [prepend (make-instrN(x86-jmp)([make-reg-op (r11) ..])..) c ..])))

(constrain generate-jump
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-jump (n c relocs r handler)
  (substitute (.. r handler)
    (and [= (@ expr-type n) (-jump-)] (do
      (if (and [= (@ expr-type (@ expr-target n)) (-continuation-)] (not (@ expr-escapes (@ expr-target n))))
        (let (cont (@ expr-target n)) (do
          (foreach (t (@ expr-arguments n)) [generate-expression t c relocs ..])
          (foreach (t (@ expr-arguments n)) (u (@ expr-parameters cont))
            (let (src-bndg (@ expr-binding-aug t)) (dest-bndg u)
              [generate-bndg-bndg-move dest-bndg src-bndg (r11) c relocs ..]))
          [prepend [make-label (@ expr-cont-instr-bndg cont) ..] c ..]
          [generate-expression (@ expr-expression cont) c relocs ..]))
      
      (if (let (cont (@ expr-short-circuit n))
          (and [not= cont null-expr] [= (@ expr-type cont) (-with-)] (not (@ expr-escapes (@ expr-target n)))
            (loop enter (current (@ expr-expression cont))
              (if [= n current]
                (true)
              (if (and [= (@ expr-type current) (-jump-)] [= (@ expr-type (@ expr-target current)) (-continuation-)])
                {enter (@ expr-expression (@ expr-target current))}
                (false))))))
        (let (dest-bndg [@car (@ expr-parameters (@ expr-short-circuit n))])
            (src-bndg (@ expr-binding-aug [@car (@ expr-arguments n)])) (do
          [generate-expression [@car (@ expr-arguments n)] c relocs ..]
          [generate-bndg-bndg-move dest-bndg src-bndg (r11) c relocs ..]))
      
      (if [not= (@ expr-short-circuit n) null-expr]
        (let (cont-instr-bndg (@ expr-cont-instr-bndg(@ expr-short-circuit n))) (do
          (if [= (@ expr-type(@ expr-target n)) (-continuation-)]
            [generate-expression (@ expr-target n) c relocs ..]
            (ignore))
          (foreach (t (@ expr-arguments n)) [generate-expression t c relocs ..])
          (foreach (t (@ expr-arguments n)) (u (@ expr-parameters(@ expr-short-circuit n)))
            (let (src-bndg (@ expr-binding-aug t)) (dest-bndg u)
              [generate-bndg-bndg-move dest-bndg src-bndg (r11) c relocs ..]))
          [prepend (make-instrN (x86-jmp) ([make-relative-relocation-immediate #4 cont-instr-bndg relocs ..]) ..) c ..]))
          
        (let (ref-val-bndg (@ expr-binding-aug (@ expr-target n))) (do
          (foreach (t (@ expr-arguments n)) [generate-expression t c relocs ..])
          [generate-expression (@ expr-target n) c relocs ..]
          [generate-register-bndg-move (r10) ref-val-bndg c relocs ..]
          [generate-buffer-to-dest n (r10) (cont-size) c relocs ..]
          [generate-load-continuation (r10) c relocs ..])))))
          
        (true)))))

(constrain initialize-jump (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-jump (n offset inits ret-val)
  (and [= (@ expr-type n) (-jump-)] (do
    [initialize-expressions (storage-cons (@ expr-target n) (@ expr-arguments n)) nil offset (hex FFFFFFFFFFFF0C30) inits ret-val]
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (setf expr-generator n [function->generator generate-jump])
    (true))))

(constrain generate-invoke
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-invoke (n c relocs r handler)
  (substitute (.. r handler)
    (and [= (@ expr-type n) (-invoke-)] (let
        (expr-val-bndg (@ expr-binding-aug n))
        (ref-expr (@ expr-target n))
        (ref-val-bndg (@ expr-binding-aug (@ expr-target n))) (do
      (ignore If the invoke expression's reference is not an absolute symbol, then put the
        target address into a register and then emit a call on that register.)
      (if (and [= (@ expr-type ref-expr) (-symbol-)] [absolute-reference-symbol? ref-expr])
        (ignore)
        [generate-expression (@ expr-target n) c relocs ..])
      (foreach (t (@ expr-arguments n)) [generate-expression t c relocs ..])
      (foreach (t [reverse (if [> [length (@ expr-arguments n)] #6] [@cddddddr(@ expr-arguments n)] nil) ..])
        (let (src-val-bndg (@ expr-binding-aug t))
          [prepend (make-instrN(x86-push) ([bndg->op src-val-bndg relocs ..]) ..) c ..]))
      
      (and [> [length (@ expr-arguments n)] #0] (do
        [generate-register-bndg-move (rdi) (@ expr-binding-aug [@car(@ expr-arguments n)]) c relocs ..]
      (and [> [length (@ expr-arguments n)] #1] (do
        [generate-register-bndg-move (rsi) (@ expr-binding-aug [@cadr(@ expr-arguments n)]) c relocs ..]
      (and [> [length (@ expr-arguments n)] #2] (do
        [generate-register-bndg-move (rdx) (@ expr-binding-aug [@caddr(@ expr-arguments n)]) c relocs ..]
      (and [> [length (@ expr-arguments n)] #3] (do
        [generate-register-bndg-move (rcx) (@ expr-binding-aug [@cadddr(@ expr-arguments n)]) c relocs ..]
      (and [> [length (@ expr-arguments n)] #4] (do
        [generate-register-bndg-move (r8) (@ expr-binding-aug [@caddddr(@ expr-arguments n)]) c relocs ..]
      (and [> [length (@ expr-arguments n)] #5]
        [generate-register-bndg-move (r9) (@ expr-binding-aug [@cadddddr(@ expr-arguments n)]) c relocs ..])))))))))))
      
      [prepend (make-instrN(x86-xor)([make-reg-op (rax) ..] [make-reg-op (rax) ..])..) c ..]
      (if (and [= (@ expr-type ref-expr) (-symbol-)] [absolute-reference-symbol? ref-expr])
        (let (ref-sym-bndg (@ expr-reference ref-expr))
          [prepend (make-instrN(x86-call)([make-relative-relocation-immediate #4 ref-sym-bndg relocs ..])..) c ..])
        [prepend (make-instrN(x86-call)([bndg->op ref-val-bndg relocs ..])..) c ..])
      [generate-bndg-register-move expr-val-bndg (rax) c relocs ..]
      
      (if [> [length (@ expr-arguments n)] #6]
        (let (drop [*(word-size)[-[length(@ expr-arguments n)]#6]])
          [prepend (make-instrN(x86-add)([make-reg-op (rsp) ..] [make-imm-op #4 drop ..])..) c ..])
        (ignore))
      (true))))))

(constrain initialize-invoke (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-invoke (n offset inits ret-val)
  (and [= (@ expr-type n) (-invoke-)] (do
    (let (call-regs (storage _ (rdi)(rsi)(rdx)(rcx)(r8)(r9)))
      (loop set-prefs (rem-args (@ expr-arguments n)) (i #0)
        (if (or [= i #6] [nil? rem-args]) (ignore) (do
          (setf bndg-preferred-storage (@ expr-binding-aug [@car rem-args]) (register-storage))
          (setf bndg-preferred-offset (@ expr-binding-aug [@car rem-args]) ($(loc (unit) i call-regs)))
          {set-prefs [@cdr rem-args] [+ i #1]}))))
    (bind (bottom-offset in-use)
      [initialize-expressions (storage-cons (@ expr-target n) (@ expr-arguments n)) nil offset (hex FFFFFFFFFFFF0031) inits ret-val] (do
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (setf expr-generator n [function->generator generate-invoke])
    (values (use-storage ret-val) bottom-offset [bor in-use (hex FFFFFFFFFFFF0FF7)])
    (true))))))

(constrain generate-if
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-if (n c relocs r handler)
  (substitute (.. r handler)
    (and [= (@ expr-type n) (-if-)]
      (let
          (expr-val-bndg (@ expr-binding-aug n))
          (condition-val-bndg (@ expr-binding-aug (@ expr-condition n)))
          (consequent-val-bndg (@ expr-binding-aug (@ expr-consequent n)))
          (alternate-val-bndg (@ expr-binding-aug (@ expr-alternate n)))
          (alternate-instr-bndg
            [make-binding-aug (extra-function-context) (absolute-storage) (local-scope) (defined-state) null-string null-frag null-expr ..])
          (end-instr-binding
            [make-binding-aug (extra-function-context) (absolute-storage) (local-scope) (defined-state) null-string null-frag null-expr ..]) (do
        [generate-expression (@ expr-condition n) c relocs ..]
        [prepend (make-instrN(x86-or)([bndg->op condition-val-bndg relocs ..][bndg->op condition-val-bndg relocs ..][make-reg-op(r11)..])..) c ..]
        [prepend (make-instrN (x86-je) ([make-relative-relocation-immediate #4 alternate-instr-bndg relocs ..]) ..) c ..]
        [generate-expression (@ expr-consequent n) c relocs ..]
        [generate-bndg-bndg-move expr-val-bndg consequent-val-bndg (r11) c relocs ..]
        [prepend (make-instrN (x86-jmp) ([make-relative-relocation-immediate #4 end-instr-binding relocs ..]) ..) c ..]
        [prepend [make-label alternate-instr-bndg ..] c ..]
        [generate-expression (@ expr-alternate n) c relocs ..]
        [generate-bndg-bndg-move expr-val-bndg alternate-val-bndg (r11) c relocs ..]
        [prepend [make-label end-instr-binding ..] c ..]
        (true))))))

(constrain initialize-if (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-if (n offset inits ret-val)
  (and [= (@ expr-type n) (-if-)] (do
    (setf expr-binding-aug (@ expr-condition n) (@ expr-binding-aug n))
    (setf expr-binding-aug (@ expr-consequent n) (@ expr-binding-aug n))
    (setf expr-binding-aug (@ expr-alternate n) (@ expr-binding-aug n))
    (bind (bottom-offset in-use) [initialize-expression (@ expr-condition n) offset inits bind]
    (bind (bottom-offset' in-use') [initialize-expression (@ expr-consequent n) offset inits bind]
    (bind (bottom-offset'' in-use'') [initialize-expression (@ expr-alternate n) offset inits bind]
    (let (in-use [bor in-use [bor in-use' [bor in-use'' (hex FFFFFFFFFFFF0830)]]]) (do
    (setf expr-generator n [function->generator generate-if])
    (values (use-storage ret-val)
      [min bottom-offset [min bottom-offset' bottom-offset'']] in-use)
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (true)))))))))

(constrain generate-constrain
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-constrain (n c relocs r handler)
  (substitute (.. r handler)
    (and [= (@ expr-type n) (-constrain-)]
      (let
          (expr-val-bndg (@ expr-binding-aug n))
          (body-val-bndg (@ expr-binding-aug (@ expr-expression n))) (do
        [generate-expression (@ expr-expression n) c relocs ..]
        [generate-bndg-bndg-move expr-val-bndg body-val-bndg (r11) c relocs ..]
        (true))))))

(constrain initialize-constrain (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-constrain (n offset inits ret-val)
  (and [= (@ expr-type n) (-constrain-)] (do
    (setf expr-binding-aug (@ expr-expression n) (@ expr-binding-aug n))
    [initialize-expressions (storage-cons (@ expr-expression n) nil) nil offset (hex FFFFFFFFFFFF0830) inits ret-val]
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (setf expr-generator n [function->generator generate-constrain])
    (true))))

(ignore
  Checks if a symbol is a value symbol, that is, a symbol that is bound to a function/
  continuation parameter.)

(constrain value-symbol? (\ r (with-vars (a) (`(function (expr) ,a)r)r)))

(function value-symbol? (n)
  (let (symbol-val-bndg (@ expr-reference n))
  (let (target-expr (@ bndg-expression symbol-val-bndg))
  (and (or [= (@ expr-type target-expr) (-function-)] [= (@ expr-type target-expr) (-continuation-)])
    [contains symbol-val-bndg (@ expr-parameters target-expr)]))))

(ignore
  Checks if a symbol is a reference symbol. A reference symbol is a symbol that is not
  a value symbol.)

(constrain reference-symbol? (\ r (with-vars (a) (`(function (expr) ,a)r)r)))

(function reference-symbol? (n) (not [value-symbol? n]))

(ignore
  Checks if a symbol is a reference symbol that is bound to an absolute location in
  memory.)

(constrain absolute-reference-symbol? (\ r (with-vars (a) (`(function (expr) ,a)r)r)))

(function absolute-reference-symbol? (n)
  (and [reference-symbol? n] [= (@ bndg-storage (@ expr-reference n)) (absolute-storage)]))

(constrain generate-symbol
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-symbol (n c relocs r handler)
  (substitute (.. r handler)
    (and [= (@ expr-type n) (-symbol-)]
      (let (expr-val-bndg (@ expr-binding-aug n))
          (symbol-val-bndg (@ expr-reference n)) (do
        (if [value-symbol? n]
          [generate-bndg-bndg-move expr-val-bndg symbol-val-bndg (r11) c relocs ..]
          [prepend (make-instrN(x86-lea)
            ([bndg->op expr-val-bndg relocs ..][bndg->op symbol-val-bndg relocs ..][make-reg-op(r11)..])..) c ..])
        (true))))))

(constrain initialize-symbol (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-symbol (n offset inits ret-val)
  (and [= (@ expr-type n) (-symbol-)] (do
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (setf expr-generator n [function->generator generate-symbol])
    (values (use-storage ret-val) offset (hex 0000000000000800))
    (true))))

(constrain generate-literal
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-literal (n c relocs r handler) (substitute (.. r handler)
  (and [= (@ expr-type n) (-literal-)]
    (let (expr-val-bndg (@ expr-binding-aug n)) (do
      [prepend(make-instrN(x86-mov)([bndg->op expr-val-bndg relocs ..][make-imm-op #8 (@ expr-value n)..][make-reg-op (r11)..])..) c ..]
      (true))))))

(constrain initialize-literal (\ r (with-vars (b c) (`(function (expr ,b (list initializer) (values ,b reg-use-vec)) ,c)r)r)))

(function initialize-literal (n offset inits ret-val)
  (and [= (@ expr-type n) (-literal-)] (do
    (setf bndg-size (@ expr-binding-aug n) (word-size))
    (setf expr-generator n [function->generator generate-literal])
    (values (use-storage ret-val) offset (hex 0000000000000800))
    (true))))

(constrain initialize-expression (\ r (with-vars (b c) (`(function (expr ,b (list initializer) ,c) (values ,b reg-use-vec))r)r)))

(function initialize-expression (n offset inits ret-val) (do (or
  (fold (found? (false)) (init inits) (or found? [[initializer->function init] n offset inits ret-val]))
  [initialize-function n offset inits ret-val]
  [initialize-continuation n offset inits ret-val]
  [initialize-with n offset inits ret-val]
  [initialize-storage n offset inits ret-val]
  [initialize-jump n offset inits ret-val]
  [initialize-invoke n offset inits ret-val]
  [initialize-if n offset inits ret-val]
  [initialize-constrain n offset inits ret-val]
  [initialize-symbol n offset inits ret-val]
  [initialize-literal n offset inits ret-val])
  ret-val))

(constrain generator->function
  (\ r (with-vars (a b) (`(function (generator)
    (function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b))r)r)))

(function generator->function (x) [cast x])

(constrain function->generator
  (\ r (with-vars (a b) (`(function
    ((function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)) generator)r)r)))

(function function->generator (x) [cast x])

(constrain make-absolute-relocation-immediate
  (\ r (with-vars (a b c) (`(function (,a ,b bndg-aug (ref (list reloc)) buffer ,c) operand)r)r)))

(function make-absolute-relocation-immediate (size addend bndg relocs r handler)
  (substitute (.. r handler)
    (let (op [make-imm-op size #0 ..]) (do
      [prepend [make-relocation
        (switch = size
          (#1 (r-x86-64-8))
          (#2 (r-x86-64-16))
          (#4 (r-x86-64-32))
          (#8 (r-x86-64-64))
          (ignore)) op bndg addend
        (function _(ctx ofst size instr-pos instr-size) ctx) ..] relocs ..]
      op))))

(constrain make-relative-relocation-immediate
  (\ r (with-vars (a b) (`(function (,a bndg-aug (ref (list reloc)) buffer ,b) operand)r)r)))

(function make-relative-relocation-immediate (size bndg relocs r handler)
  (substitute (.. r handler)
    (let (op [make-imm-op size #0 ..]) (do
      [prepend [make-relocation
        (switch = size
          (#1 (r-x86-64-pc8))
          (#2 (r-x86-64-pc16))
          (#4 (r-x86-64-pc32))
          (#8 (r-x86-64-pc64))
          (ignore)) op bndg (ignore)
        (function _(ctx ofst size instr-pos instr-size)
          [- ofst [+ instr-pos instr-size]]) ..] relocs ..]
      op))))

(constrain make-label (\ r (with-vars (a) (`(function (bndg-aug buffer ,a) instr)r)r)))

(function make-label (bndg r handler)
  (substitute (.. r handler)
    (let (lbl (make-instrN (label) () ..)) (do
      (setf instr-binding lbl bndg)
      lbl))))

(constrain bndg->partial-op (\ r (with-vars (a b) (`(function (,b bndg-aug (ref (list reloc)) buffer ,a) operand)r)r)))

(function bndg->partial-op (op-size bndg relocs buf handler)
  (substitute (.. buf handler)
    (if [= (@ bndg-storage bndg) (absolute-storage)]
      (let (op [make-mem-op op-size (rip) (none) #0 #0 #4 ..]) (do
        [prepend [make-relocation (r-x86-64-pc32) op bndg (ignore)
          (function _(ctx ofst size instr-pos instr-size)
            [- ofst [+ instr-pos instr-size]]) ..] relocs ..]
        op))
    
    (if (and [= (@ bndg-storage bndg) (frame-relative-storage)] [= (@ bndg-state bndg) (defined-state)])
      [make-mem-op op-size (rbp) (none) #0 (@ bndg-offset bndg) #4 ..]
    
    (if [= (@ bndg-storage bndg) (register-storage)]
      [make-reg-op
        [(switch = op-size
          (#1 reg-lower-8)
          (#2 reg-lower-16)
          (#4 reg-lower-32)
          (#8 reg-lower-64)
          (ignore)) (@ bndg-offset bndg)] ..]
    
      (ignore))))))

(constrain bndg->op (\ r (with-vars (a) (`(function (bndg-aug (ref (list reloc)) buffer ,a) operand)r)r)))

(function bndg->op (bndg relocs buf handler)
  [bndg->partial-op (@ bndg-size bndg) bndg relocs buf handler])

(constrain generate-bndg-bndg-move
  (\ r (with-vars (a b) (`(function (bndg-aug bndg-aug register (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-bndg-bndg-move (dst src tmp c relocs r handler)
  (substitute (.. r handler)
    (if (and [= (@ bndg-storage dst) (@ bndg-storage src)]
        [= (@ bndg-offset dst) (@ bndg-offset src)]
        [= (@ bndg-size dst) (@ bndg-size src)])
      (ignore)
      [prepend (make-instrN(x86-mov)([bndg->op dst relocs ..][bndg->op src relocs ..][make-reg-op tmp ..])..) c ..])))

(constrain generate-register-bndg-move
  (\ r (with-vars (a b) (`(function (register bndg-aug (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-register-bndg-move (dst src c relocs r handler)
  (substitute (.. r handler)
    (if (and [= (@ bndg-storage src) (register-storage)] [= dst (@ bndg-offset src)])
      (ignore)
      [prepend (make-instrN(x86-mov)([make-reg-op dst ..][bndg->op src relocs ..])..) c ..])))

(constrain generate-bndg-register-move
  (\ r (with-vars (a b) (`(function (bndg-aug register (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-bndg-register-move (dst src c relocs r handler)
  (substitute (.. r handler)
    (if (and [= (@ bndg-storage dst) (register-storage)] [= src (@ bndg-offset dst)])
      (ignore)
      [prepend (make-instrN(x86-mov)([bndg->op dst relocs ..][make-reg-op src ..])..) c ..])))

(constrain generate-expression
  (\ r (with-vars (a b) (`(function (expr (ref (list instr)) (ref (list reloc)) buffer ,a) ,b)r)r)))

(function generate-expression (n c relocs r handler)
  (substitute (.. r handler)
    [[generator->function (@ expr-generator n)] n c relocs ..]))

(constrain generate-program
  (\ r (with-vars (a b) (`(function ((list expr) ,b buffer ,a) (values (list instr) (list reloc)))r)r)))

(function generate-program (exprs outs r handler)
  (substitute (.. r handler)
    (let (c (storage _ nil)) (relocs (storage _ nil)) (do
    [prepend (make-instrN(x86-push)([make-reg-op (rbp) ..])..) c ..]
    [prepend (make-instrN(x86-mov)([make-reg-op (rbp) ..] [make-reg-op (rsp) ..])..) c ..]
    (foreach (expr exprs) [generate-expression expr c relocs ..])
    [prepend (make-instrN(x86-leave)()..) c ..]
    [prepend (make-instrN(x86-ret)()..) c ..]
    (values (use-storage outs) [reverse $c ..] $relocs)))))

